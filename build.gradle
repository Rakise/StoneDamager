plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2' // for shadowing
    id "io.freefair.lombok" version "8.6"
}

apply from: rootDir.toString() + '/dependencies.gradle'

group = properties['group']
version = properties['version']

java.sourceCompatibility = JavaVersion.VERSION_11
java.targetCompatibility = JavaVersion.VERSION_11

ext {
    pluginMain = properties['plugin.main'] == 'default' ?
            "${project.group}.${project.name.toLowerCase()}.${project.name}" :
            properties['plugin.main']
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    
    // Updated JitPack repo logic to be conditional
    // This prevents 401 errors on public repos if the authToken is empty
    maven { 
        url "https://jitpack.io" 
        if (project.hasProperty('authToken') && project.authToken.toString().trim()) {
            credentials { 
                username = project.authToken 
            }
        }
    }
    
    maven {
        name = "placeholderapi"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven { url 'https://repo.papermc.io/repository/maven-snapshots/' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}

disableAutoTargetJvm()

dependencies {
    // Defaults.
    compileOnly(files(FILES))
    annotationProcessor(ANNO)
    implementation(IMPL)
    compileOnly(COMP_ONLY)
    shadow(SHADOW)

    // Other Plugins
    compileOnly(OTHER_PLUGINS)
}

processResources {
    // Debugging: Print values
    doFirst {
        println "Version: ${project.version}, Name: ${project.name}, Main: ${project.ext.pluginMain}"
    }

    inputs.property('name', "${project.name}")
    inputs.property('version', "${project.version}")
    inputs.property('main', "${project.ext.pluginMain}")

    filesMatching('**/plugin.yml') {
        expand (
                'name': "${project.name}",
                'version': "${project.version}",
                'main': "${project.ext.pluginMain}",
        )
    }
}

shadowJar {
    archiveClassifier.set('')

    buildDir = "target/"
    archiveFileName = "${project.name}-${project.version}.jar"

    relocate('com.github.Anon8281.universalScheduler', 'net.streamline.libs.universalScheduler')
}

artifacts {
    archives shadowJar
}

wrapper {
    gradleVersion = '8.9'
    distributionType = Wrapper.DistributionType.ALL
}
